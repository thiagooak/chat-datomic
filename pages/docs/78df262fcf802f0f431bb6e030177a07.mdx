Client API Tutorial
===================

This tutorial introduces the Datomic Client API. You will

- [create a database](#create-database)
- [transact schema](#transact-schema)
- [transact data](#transact-data)
- [query the database](#query)
- optionally [delete the database](#delete-database) when you are done

Prerequisites
-------------

This tutorial assumes that you have [setup dev-local](../dev-local.html) and started a REPL with dev-local on your classpath, or you have [launched](../getting-started/start-system.html) a Datomic Cloud system and know how to start a Clojure REPL with the [Datomic Client API installed](../client/client-api.html#installing).





Create a Client
---------------



### Using dev-local:

To interact with Datomic, you must first create a [datomic.client.api/client](https://docs.datomic.com/client-api/datomic.client.api.html#var-client).

In your REPL, execute:

noslide

```clojure
(require '[datomic.client.api :as d])

(def cfg {:server-type :dev-local
          :system "datomic-samples"})

(def client (d/client cfg))
```







### Using Datomic Cloud:

To [connect](,,/client/client-api.html#connection) to Datomic Cloud, you will need the following information:

- `:region` is the AWS region in which you've started Datomic Cloud.
- `:system` is your Datomic system's name.
- `:endpoint` is your system's client endpoint. Check your [CloudFormation outputs](../operation/howto.html#template-outputs) for `ClientApiGatewayEndpoint`.

Use this information to create a client with [datomic.client.api/client](https://docs.datomic.com/client-api/datomic.client.api.html#var-client):

Even though the endpoint is public, client access is securely managed by [IAM permissions](../operation/access-control.html#how-datomic-access-control-works).

noslide

```clojure
(require '[datomic.client.api :as d])

(def cfg {:server-type :ion
          :region "<your AWS Region>" ;; e.g. us-east-1
          :system "<system name>"
          :creds-profile "<your_aws_profile_if_not_using_the_default>"
          :endpoint "<your endpoint>"})

(def client (d/client cfg))
```



Warnings may occur. Do not be alarmed as they will not affect functionality during this tutorial.







Create a Database
-----------------

Create a new database with [datomic.client.api/create-database](https://docs.datomic.com/client-api/datomic.client.api.html#var-create-database).

noslide

```clojure
(d/create-database client {:db-name "movies"})
```



Now you're ready to connect to your newly created database using [dataomic.client.api/connect](https://docs.datomic.com/client-api/datomic.client.api.html#var-connect).

noslide

```clojure
(def conn (d/connect client {:db-name "movies"}))
```



The next step will be to define some schema for your new database.

Schema defines the set of possible attributes that can be associated with an entity. We'll [need to provide](../schema/defining-schema.html) 3 attributes: [db/ident](../schema/schema-reference.html#db-ident), [db/valueType](../schema/schema-reference.html#db-valuetype) and [db/cardinality](../schema/schema-reference.html#db-cardinality). [db/doc](../schema/schema-reference.html#db-doc) will also be provided for documentation.





Transact Schema
---------------

Now we need to create a [schema](../schema/schema.html).

Define the following small schema for a database about movies:

noslide

```clojure
(def movie-schema [{:db/ident :movie/title
                    :db/valueType :db.type/string
                    :db/cardinality :db.cardinality/one
                    :db/doc "The title of the movie"}

                   {:db/ident :movie/genre
                    :db/valueType :db.type/string
                    :db/cardinality :db.cardinality/one
                    :db/doc "The genre of the movie"}

                   {:db/ident :movie/release-year
                    :db/valueType :db.type/long
                    :db/cardinality :db.cardinality/one
                    :db/doc "The year the movie was released in theaters"}])
```



Now transact the schema using [datomic.client.api/transact](https://docs.datomic.com/client-api/datomic.client.api.html#var-transact).

noslide

```clojure
(d/transact conn {:tx-data movie-schema})
```



```clojure
=>
{:db-before ..., 
 :db-after ..., 
 :tx-data [...], ;; data added to the database
 :tempids {}}
```



You should get back [a response](../transactions/transaction-processing.html#results) as shown above.





Transact Data
-------------

Now you can define some movies to add to the database utilizing the [schema](../schema/defining-schema.html) we defined earlier:

noslide

```clojure
(def first-movies [{:movie/title "The Goonies"
                    :movie/genre "action/adventure"
                    :movie/release-year 1985}
                   {:movie/title "Commando"
                    :movie/genre "thriller/action"
                    :movie/release-year 1985}
                   {:movie/title "Repo Man"
                    :movie/genre "punk dystopia"
                    :movie/release-year 1984}])
```



[Transact](../transactions/transaction-processing.html) the movies into the database:

noslide

```clojure
(d/transact conn {:tx-data first-movies})
```



```clojure
=>
{:db-before ... 
 :db-after ...
 :tx-data [...], 
 :tempids {}}
```



You should see a response similar to the above with different data.





Query
-----

Get a current value for the database with [datomic.client.api/db](https://docs.datomic.com/client-api/datomic.client.api.html#var-db):

noslide

```clojure
(def db (d/db conn))  
```



Now create a [query](../query/query-executing.html) for all movie titles:

noslide

```clojure
(def all-titles-q '[:find ?movie-title 
                    :where [_ :movie/title ?movie-title]])
```



And execute the query with the value of the database using [datomic.client.ap/q](https://docs.datomic.com/client-api/datomic.client.api.html#var-q):

noslide

```clojure
(d/q all-titles-q db)
```



```clojure
=>
[["Commando"] ["The Goonies"] ["Repo Man"]]
```



If your database had a large number of movies, it may be prudent to use [qseq](../query-executing.html#qseq) to return a lazy sequence quickly rather than waiting for the full results to build and return.





Delete a Database (Optional)
----------------------------

When you are done with this tutorial, you can use [datomic.client.api/delete-database](https://docs.datomic.com/client-api/datomic.client.api.html#var-delete-database) to delete the *movies* database:

noslide

```clojure
(d/delete-database client {:db-name "movies"})
```